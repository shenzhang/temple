ext {
    springVersion = '4.0.3.RELEASE'
    metricsVersion = '3.0.1'
    deployDir = "$buildDir/deploy"
    distDir = "dist"
}

buildscript {
    repositories {
        maven { url 'http://127.0.0.1:8081/nexus/content/groups/public' }   // comment this line if you don't have a private nexus repo
        mavenCentral()
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'

    repositories {
        maven { url 'http://127.0.0.1:8081/nexus/content/groups/public' } // comment this line if you don't have a private nexus repo
        mavenCentral()
    }

    idea {
        project {
            jdkName = '1.7'
            languageLevel = '1.7'
        }
    }
}

apply plugin: 'jetty'
apply from: 'config/database/database.gradle'

dependencies {
    compile(
            'commons-logging:commons-logging:1.1.3',
            'org.slf4j:slf4j-api:1.7.5',
            'org.slf4j:jcl-over-slf4j:1.7.5',
            'org.slf4j:slf4j-log4j12:1.7.6',
            'com.google.guava:guava:15.0',
            'commons-beanutils:commons-beanutils:1.9.2',
            'log4j:log4j:1.2.17',
            'javax.servlet.jsp.jstl:jstl:1.2',
            'javax.servlet:servlet-api:2.5',
            'javax.servlet.jsp:jsp-api:2.2',
            'org.apache.tiles:tiles-core:3.0.4',
            'org.apache.tiles:tiles-jsp:3.0.4',
            "com.codahale.metrics:metrics-core:$metricsVersion",
            "com.codahale.metrics:metrics-annotation:$metricsVersion",
            "com.codahale.metrics:metrics-servlets:$metricsVersion",
            "com.codahale.metrics:metrics-jvm:$metricsVersion",

            'com.github.shenzhang:easy-jdbc:0.1.1',
            "org.springframework:spring-webmvc:$springVersion",
            "org.springframework:spring-jdbc:$springVersion",
            'org.springframework.security:spring-security-web:3.2.5.RELEASE',
            'org.springframework.security:spring-security-config:3.2.5.RELEASE',

            'org.hibernate:hibernate-validator:5.1.2.Final',
            'joda-time:joda-time:2.4',
            'com.fasterxml.jackson.core:jackson-databind:2.4.2'
    )

    testCompile(
            'junit:junit:4.11',
			'org.easymock:easymock:3.2',
            "org.springframework:spring-test:$springVersion"
    )
}

sourceSets {
    main {
        resources {
            srcDir 'deploy/properties'
        }
    }
}

war {
    rootSpec.exclude 'log4j.properties', 'temple.properties'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}

jettyRun {
    contextPath = ''
}

// build and deploy the application
task deploy(dependsOn: 'build') << {
    delete "$deployDir"

    copy {
        from 'deploy'
        into "$deployDir"
    }

    copy {
        from "$buildDir/libs"
        into "$deployDir"
        include '*.war'
    }

    delete "$distDir"
    copy {
        from "$buildDir/libs"
        into "$distDir"
        include '*.war'
    }
}

def defaultProperty(name, defaultValue) {
    hasProperty(name) ? property(name) : defaultValue
}
