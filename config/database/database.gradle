import groovy.sql.Sql

ext {
    databaseType = defaultProperty('db', 'sqlite')

    println "loading database type: ${databaseType}..."

    schemaDir = file("config/database/$databaseType/schema")
    dataDir = file("config/database/$databaseType/data")
}

configurations {
    driver
}

apply from: "config/database/$databaseType/db.gradle"

task loadDriver << {
    URLClassLoader loader = GroovyObject.class.classLoader
    configurations.driver.each { File file ->
        loader.addURL(file.toURL())
    }
}

def executeSql(connection, sql) {
    sql = sql.trim()
    if (sql.length() > 0) {
        println(sql)
        connection.execute(sql)
    }
}

def createSchema(connection, fileName) {
    println "begin create schema from $fileName"

    def file = new File(schemaDir, fileName)
    def sql = new StringBuilder()
    file.eachLine {
        sql.append(it)
    }

    sql.toString().split(';').each() {
        executeSql(connection, it)
    }
}

def importData(connection, fileName) {
    println "import data from $fileName"

    def file = new File(dataDir, fileName)
    def sql = new StringBuilder()
    file.eachLine {
        sql.append(it)
    }

    sql.toString().split(';').each() {
        executeSql(connection, it);
    }
}


task createDB(dependsOn: loadDriver) << {
    println 'begin create database...\n'

    dropDatabase();

    createDatabase();

    def connection = Sql.newInstance("$dbUrl", "$dbUser", "$dbPassword", "$dbDriver")

    createSchema(connection, 'create-db.sql')

    println '\n'

    importData(connection, 'user.sql');
    importData(connection, 'temple.sql');
    importData(connection, 'city.sql');
    importData(connection, 'member.sql');

    connection.close()

    println '\ndone'
}